# Maintainer: Maksim Bondarenkov <maksapple2306@gmail.com>

_realname=libtg_owt
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
# It seems the currently desired version can be gotten from
# https://github.com/telegramdesktop/tdesktop/blob/dev/Telegram/build/docker/centos_env/Dockerfile
_commit=8198c4d8b91e22d68eb5c7327fd408e3b6abcc79
pkgver=r463.8198c4d
pkgrel=1
pkgdesc='WebRTC library (mingw-w64)'
arch=('any')
mingw_arch=('ucrt64' 'clang64')
url='https://github.com/desktop-app/tg_owt'
license=('spdx:BSD-3-Clause')
makedepends=("${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-ninja"
             "${MINGW_PACKAGE_PREFIX}-protobuf"
             "${MINGW_PACKAGE_PREFIX}-openssl"
             "${MINGW_PACKAGE_PREFIX}-ffmpeg"
             "${MINGW_PACKAGE_PREFIX}-libva"
             "${MINGW_PACKAGE_PREFIX}-opus"
             "${MINGW_PACKAGE_PREFIX}-yasm"
             "${MINGW_PACKAGE_PREFIX}-libjpeg-turbo"
             "${MINGW_PACKAGE_PREFIX}-libepoxy"
             "${MINGW_PACKAGE_PREFIX}-openh264"
             'unzip'
             'git')
source=("git+${url}.git#commit=${_commit}"
        "libtg_owt_mingw_build.patch")
sha256sums=('a06d346d9a9baeb10293caf63b03c4dca529174b308ba91e8ac99e91d1434eff'
            '5e79f384eb1c6e22473a82f3fac56cef8fbfe926544eab831bb6da052d25d75d')

pkgver() {
  printf 'r%s.%s' "$(git -C tg_owt rev-list --count ${_commit})" "${_commit:0:7}"
}

prepare() {
  cd tg_owt
  git submodule update --init --recursive

  patch -Np1 -i "${srcdir}"/libtg_owt_mingw_build.patch
}

build() {
  # Upstream stated that only static builds are really supported so that's what we'll do.
  # https://github.com/desktop-app/tg_owt/issues/75#issuecomment-992061171
  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX="
    cmake \
      -S tg_owt \
      -B "build-${MSYSTEM}" \
      -G Ninja \
      -DCMAKE_BUILD_TYPE=Release \
      -DCMAKE_INSTALL_PREFIX="${MINGW_PREFIX}" \
      -DBUILD_SHARED_LIBS=OFF

  cmake --build "build-${MSYSTEM}"
}

package() {
  DESTDIR="${pkgdir}" cmake --install "build-${MSYSTEM}"

  install -Dm644 tg_owt/LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE"
}
